apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.siziksu.tmdb"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 2515001
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // Disable signing if there is no available keystore (local development)
    def properSigningConfig = android.signingConfigs.debug
    // KeyStore and KeyAlias Password. Uses environment variables to resolve.
    def keyStorePass = "$System.env.KEY_STORE_PASS"
    def keyAliasName = "$System.env.KEY_ALIAS"
    def keyAliasPassword = "$System.env.KEY_ALIAS_PASS"
    // keyStorePath should point to the .jks file
    def keyStorePath = "$System.env.KEY_PATH"
    signingConfigs {
        release {
            storeFile file(keyStorePath)
            storePassword keyStorePass
            keyAlias keyAliasName
            keyPassword keyAliasPassword
        }
    }
    if (keyStorePath != "null") {
        // Set signing config to use this keystore
        properSigningConfig = signingConfigs.release
    }
    buildTypes {
        buildTypes.each {
            it.buildConfigField "String", "API_PUBLIC_KEY", the_movie_database_api_key
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig properSigningConfig
        }
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    testCompile 'junit:junit:4.12'
    testCompile 'org.powermock:powermock-module-junit4:1.6.2'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.6.2'
    testCompile 'org.powermock:powermock-api-mockito:1.6.2'
    testCompile 'org.powermock:powermock-classloading-xstream:1.6.2'

    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'

    compile 'com.squareup.picasso:picasso:2.5.2'

    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'

    compile 'com.google.dagger:dagger:2.8'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.8'
}
